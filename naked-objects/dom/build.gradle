dependencies {
    compile 'org.apache.isis.core:isis-core-applib:1.9.0'
    compile 'org.datanucleus:datanucleus-core:4.1.2'
    compile 'org.datanucleus:datanucleus-jodatime:4.1.0-release'
    compile 'org.datanucleus:datanucleus-api-jdo:4.1.0-release'
    testCompile 'org.apache.isis.core:isis-core-unittestsupport:1.9.0'
    testCompile 'org.junit.vintage:junit-vintage-engine:4.12.2'
    testCompile 'org.objenesis:objenesis:1.4'
    testCompile 'org.assertj:assertj-core:2.0.0'
}

task datanucleusEnhance {
    description "Enhance JPA model classes using DataNucleus Enhancer"
    dependsOn compileJava

    doLast {
        // define the entity classes
        def entityFiles = fileTree(sourceSets.main.java.outputDir).matching {
            include 'domainapp/dom/**/*.class'
        }

        println "Enhancing with DataNucleus the following files"
        entityFiles.getFiles().each {
            println it
        }

        // define Ant task for DataNucleus Enhancer
        ant.taskdef(
                name: 'datanucleusenhancer',
                classpath: sourceSets.main.runtimeClasspath.asPath,
                classname: 'org.datanucleus.enhancer.EnhancerTask'
                // the below is for DataNucleus Enhancer 3.1.1
                //classname : 'org.datanucleus.enhancer.tools.EnhancerTask'
        )

        // run the DataNucleus Enhancer as an Ant task
        ant.datanucleusenhancer(
                classpath: sourceSets.main.runtimeClasspath.asPath,
                verbose: true,
                api: "JDO") {
            entityFiles.addToAntBuilder(ant, 'fileset', FileCollection.AntType.FileSet)
        }
    }
}

classes.dependsOn(datanucleusEnhance)