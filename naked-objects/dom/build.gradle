plugins {
    id 'java-library'
}
dependencies {
    api libs.isis_core_applib
    implementation 'org.datanucleus:datanucleus-core:4.1.2'
    implementation 'org.datanucleus:datanucleus-jodatime:4.1.0-release'
    implementation 'org.datanucleus:datanucleus-api-jdo:4.1.0-release'
    testImplementation libs.isis_core_unittestsupport
    testImplementation libs.junit_vintage_engine
    testImplementation 'org.objenesis:objenesis:1.4'
    testImplementation libs.assertj_core
}

task datanucleusEnhance {
    description "Enhance JPA model classes using DataNucleus Enhancer"
    dependsOn compileJava

    doLast {
        // define the entity classes
        def entityFiles = fileTree(sourceSets.main.java.outputDir).matching {
            include 'domainapp/dom/**/*.class'
        }

        println "Enhancing with DataNucleus the following files"
        entityFiles.getFiles().each {
            println it
        }

        // define Ant task for DataNucleus Enhancer
        ant.taskdef(
                name: 'datanucleusenhancer',
                classpath: sourceSets.main.runtimeClasspath.asPath,
                classname: 'org.datanucleus.enhancer.EnhancerTask'
                // the below is for DataNucleus Enhancer 3.1.1
                //classname : 'org.datanucleus.enhancer.tools.EnhancerTask'
        )

        // run the DataNucleus Enhancer as an Ant task
        ant.datanucleusenhancer(
                classpath: sourceSets.main.runtimeClasspath.asPath,
                verbose: true,
                api: "JDO") {
            entityFiles.addToAntBuilder(ant, 'fileset', FileCollection.AntType.FileSet)
        }
    }
}

classes.dependsOn(datanucleusEnhance)